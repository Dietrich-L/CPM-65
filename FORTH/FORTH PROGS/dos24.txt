DOS23.blk

SCR # 3
  0 ( FORTH DOS V2.3  PRIMITIVE DOS VIA CP/M-65 BDOS )
  1 VOCABULARY DOS IMMEDIATE HEX DOS DEFINITIONS
  2 : FCB1 F6 @ ;
  3 : FCB2 F4 @ ;
  4 : DIR-BUF FC @ ;
  5 : SET-DMA FE ! ;
  6 : ?DISK-ERR IF ." Disk Error " HEX 0 <# # # #>
  7                TYPE CR ABORT ENDIF ;
  8 FORTH DEFINITIONS
  9 : UCASE DUP 60 > OVER 7B < AND IF 5F AND ENDIF ;
 10 : 2DUP over over ;
 11 : 2SWAP rot >R rot R> ;
 12 DOS DEFINITIONS
 13
 14 -->
 15

SCR # 4
  0 ( BDOS COMMANDS  )
  1 : RESET 0 0D BDOS ?DISK-ERR DROP ;
  2 : SELECT 0E BDOS ?DISK-ERR DROP ;
  3 : OPEN-FILE 0 0F BDOS ;
  4 : CLOSE     0 10 BDOS ?DISK-ERR DROP ;
  5 : SEARCH0   0 11 BDOS >R 0 100 U/ SWAP DROP R> ?DISK-ERR ;
  6 : SEARCH    0 12 BDOS >R 0 100 U/ SWAP DROP R> ;
  7 : READ      0 14 BDOS ;
  8 : WRITE     0 15 BDOS ?DISK-ERR DROP ;
  9 : MAKE-FILE 0 16 BDOS ;
 10
 11 -->
 12
 13
 14
 15

SCR # 5
  0 ( FCB HANDLING )
  1 : (!FCB)        ( ADDR LEN FCB-ADDR -   )
  2   DUP 10 ERASE DUP 1+ 0B BLANKS    ( LOESCHT FCB )
  3   >R OVER 1+ C@ 3A =              ( PRUEFT ": )
  4   IF OVER C@ 40 - R C! 2 - SWAP 2 + SWAP ENDIF ( DRIVE )
  5   R> 1+ ROT ROT 0 DO           ( SETZE FILENAME )
  6     DUP C@ 2E = IF SWAP 8 I - +  ( WENN . )
  7       ELSE 2DUP C@ UCASE SWAP C! SWAP 1+ ENDIF
  8   SWAP 1+ LOOP DROP DROP ;      ( DROP FCB-ADDR ADDR )
  9
 10 : !FCB BL WORD HERE COUNT ROT (!FCB) ;
 11 : .NAME DIR-BUF + 1+ 8 2DUP   ( PRINT FILENAME )
 12     TYPE 2E EMIT + 3 TYPE 4 SPACES ;
 13 : .FCB CR 0C 0 DO DUP C@ EMIT 1+ LOOP DROP ;
 14 -->
 15

SCR # 6
  0 ( SYSTEM COMMANDS FOR FORTH )
  1 FORTH DEFINITIONS
  2 : BYE 0 0 BDOS ;
  3 : (") R COUNT DUP 1+ R> + >R ;
  4 : " 22 COMPILE (") WORD HERE C@ 1+ ALLOT ;
  5 IMMEDIATE
  6 : SAVE-SYSTEM      ( SAVE SYSTEM IMAGE OF ACTUAL FORTH )
  7     DOS FCB1 !FCB MAKE-FILE ?DISK-ERR DROP
  8     HERE 200 DO I SET-DMA WRITE 100 +LOOP CLOSE ;
  9 : DIR " ????????.???" DOS FCB1 (!FCB) CR SEARCH0
 10     BEGIN .NAME SEARCH UNTIL DROP ;
 11 : A: 0 DOS SELECT ;       : D: 3 DOS SELECT ;
 12 : B: 1 DOS SELECT ;       : E: 4 DOS SELECT ;
 13 : C: 2 DOS SELECT ;       : F: 5 DOS SELECT ;
 14 DECIMAL
 15 -->

SCR # 7
  0 ( NDOS.BLK   MORE DOS DEFINITIONS )
  1 DOS DEFINITIONS HEX
  2 : OM-FILE OPEN-FILE IF DUP DD = IF MAKE-FILE ?DISK-ERR DROP
  3     ELSE 1 ?DISK-ERR ENDIF ENDIF DROP ;
  4 : (SAVE)
  5   FCB1 !FCB FLUSH OM-FILE
  6   DO I SET-DMA WRITE B/BUF +LOOP CLOSE ;
  7 FORTH DEFINITIONS HEX
  8 : OPEN
  9   DOS FCB1 !FCB OPEN-FILE ?DISK-ERR DROP LO DUP SET-DMA EOM !
 10   BEGIN READ SWAP OVER IF DUP D7 =
 11   IF DROP EOM @ B/BUF - EOM ! ELSE SWAP ?DISK-ERR ENDIF ELSE
 12   DROP EOM @ B/BUF + DUP EOM ! SET-DMA ENDIF UNTIL
 13   EMPTY-BUFFERS ;
 14 : SAVE EOM @ 100 + LO DOS (SAVE) ;
 15 -->

SCR # 8
  0 ( DOS UTILITIES )
  1 : KILL    ( DELETE FILES )
  2   DOS FCB1 !FCB 0 13 BDOS ?DISK-ERR DROP ;
  3
  4 -->
  5
  6
  7
  8
  9
 10
 11
 12
 13
 14
 15

SCR # 9
  0 ( MORE DOS )
  1
  2 FORTH DEFINITIONS DECIMAL
  3 LATEST   12 +ORIGIN !   ( TOP NFA )
  4 ( HERE     28 +ORIGIN !   ( FENCE )
  5 HERE     30 +ORIGIN !   ( DP )
  6 ' DOS     6 +ORIGIN !   ( VOC-LINK )
  7 ( HERE FENCE ! )  ;S
  8
  9
 10
 11
 12
 13
 14
 15

