;fig-FORTH
;by D. Lausberg    (c) 1990
;V1.0	7.1.90
;V1.1  21.1.95		some improvements, debug code removed
;V1.2  20.02.95		RAM-disk version, BDOS
;V1.3  15.04.96		PRINTER
;V1.4  26.04.96		ENCLOSE fixed, upper case (FIND)
;V1.5  08.05.21		PRTCHR masks bit7 (AND $7F)
;V1.6  17.05.21		SCR# 3, ?STACK UM* U/ U< R/W errors, U* faster algo
;			stx xsave in XQTER eliminated
Version = $16

;--- CONSTANTS ---
BS	= $08			;backspace
SSIZE	= 256
NSEC	= 16
NBUF	= 4
BMAG	= SSIZE+4*NBUF

;--- System Addresses
BOS	= $10
TOS	= $C0
N	= TOS+8
IP	= N+8
W	= IP+3
UP	= W+2
XSAVE	= UP+2

INBUFV	= $EC
BDOSJP	= $F0
DMA	= $FE

TIBX	= $100
ORIG	= $200
MEM	= $8000
UAREA	= MEM-128
DAREA	= UAREA-BMAG
LOWMEM	= MEM			;ramdisc
HIMEM	= $D7FF			;screen #3 .. #21

;--- BDOS Commands ---
BD_PRT	= $05


	ORG ORIG

ENTER	NOP
	JMP COLD+2
REENTR	NOP
	JMP WARM
	DW $0004,$5ED2,NTOP,BS
	DW UAREA,TOS,$1FF,TIBX,31
	DW 0			;0=no disk  1=disk warnings
	DW TOP,TOP,VLO


L22	DB $83,'LI',$D4
	DW 0
LIT	DW LIT+2
	LDA (IP),Y
	PHA
	INC IP
	BNE L30
	INC IP+1
L30	LDA (IP),Y
L31	INC IP
	BNE PUSH
	INC IP+1

PUSH	DEX
	DEX

PUT	STA 1,X
	PLA
	STA 0,X

NEXT	LDY #1			;NEXT
	LDA (IP),Y
	STA W+1
	DEY
	LDA (IP),Y
	STA W
	CLC
	LDA IP
	ADC #2
	STA IP
	BCC L54
	INC IP+1
L54	JMP W-1


L35	DB $84,'CLI',$D4	;CLIT
	DW L22
CLIT	DW CLIT+2
	LDA (IP),Y
	PHA
	TYA
	BEQ L31


SETUP	ASL A			;put A args from TOS to N
	STA N-1
L63	LDA 0,X			;
	STA N,Y
	INX
	INY
	CPY N-1
	BNE L63
	LDY #0
	RTS


L75	DB $87,'EXECUT',$C5	;EXECUTE
	DW L35
EXEC	DW EXEC+2
	LDA 0,X
	STA W
	LDA 1,X
	STA W+1
	INX
	INX
	JMP W-1


L89	DB $86,'BRANC',$C8	;BRANCH
	DW L75
BRAN	DW BRAN+2
	CLC
	LDA (IP),Y
	ADC IP
	PHA
	INY
	LDA (IP),Y
	ADC IP+1
	STA IP+1
	PLA
	STA IP
	JMP NEXT+2


L107	DB $87,'0BRANC',$C8	;0BRANCH
	DW L89
ZBRAN	DW ZBRAN+2
	INX
	INX
	LDA $FE,X
	ORA $FF,X
	BEQ BRAN+2

BUMP	CLC
	LDA IP
	ADC #2
	STA IP
	BCC L122
	INC IP+1
L122	JMP NEXT


L127	DB $86,'(LOOP',$A9	;(LOOP)
	DW L107
PLOOP	DW PLOOP+2
	STX XSAVE
	TSX
	INC $101,X
	BNE PL1
	INC $102,X

PL1	CLC
	LDA $103,X
	SBC $101,X
	LDA $104,X
	SBC $102,X

PL2	LDX XSAVE
	ASL A
	BCC BRAN+2
	PLA
	PLA
	PLA
	PLA
	JMP BUMP


L154	DB $87,'(+LOOP',$A9
	DW L127
PPLOO	DW PPLOO+2
	INX
	INX
	STX XSAVE
	LDA $FF,X
	PHA
	PHA
	LDA $FE,X
	TSX
	INX
	INX
	CLC
	ADC $101,X
	STA $101,X
	PLA
	ADC $102,X
	STA $102,X
	PLA
	BPL PL1
	CLC
	LDA $101,X
	SBC $103,X
	LDA $102,X
	SBC $104,X
	JMP PL2


L185	DB $84,'(DO',$A9	;(DO)
	DW L154
PDO	DW PDO+2
	LDA 3,X
	PHA
	LDA 2,X
	PHA
	LDA 1,X
	PHA
	LDA 0,X
	PHA
POPTWO	INX
	INX
POP	INX
	INX
	JMP NEXT


L207	DB $81,$C9
	DW L185
I	DW R+2


L214	DB $85,'DIGI',$D4
	DW L207
DIGIT	DW DIGIT+2
	SEC
	LDA 2,X
	SBC #$30
	BMI L234
	CMP #$A
	BMI L227
	SEC
	SBC #7
	CMP #$A
	BMI L234
L227	CMP 0,X
	BPL L234
	STA 2,X
	LDA #1
	PHA
	TYA
	JMP PUT

L234	TYA
	PHA
	INX
	INX
	JMP PUT


L243	DB $86,'(FIND',$A9
	DW L214
PFIND	DW PFIND+2
	LDA #2
	JSR SETUP
	STX XSAVE
L249	LDY #0
	LDA (N),Y
	EOR (N+2),Y
	AND #$3F
	BNE L281
L254	INY
	LDA (N+2),Y
	CMP #'a
	BCC L255
	CMP #'{
	BCS L255
	AND #$5F		;lower -> upper case
L255	EOR (N),Y
	ASL A
	BNE L280
	BCC L254
	LDX XSAVE
	DEX
	DEX
	DEX
	DEX
	CLC
	TYA
	ADC #5
	ADC N
	STA 2,X
	LDY #0
	TYA
	ADC N+1
	STA 3,X
	STY 1,X
	LDA (N),Y
	STA 0,X
	LDA #1
	PHA
	JMP PUSH
L280	BCS L284
L281	INY
	LDA (N),Y
	BPL L281
L284	INY
	LDA (N),Y
	TAX
	INY
	LDA (N),Y
	STA N+1
	STX N
	ORA N
	BNE L249
	LDX XSAVE
	LDA #0
	PHA
	JMP PUSH

	;DS 2

L301	DB $87,'ENCLOS',$C5	;enclose adr1 c --> adr1 n1 n2 n3
	DW L243
ENCL	DW ENCL+2
	LDA #2
	JSR SETUP		; 2 args from Stack to N
	TXA
	SEC
	SBC #8			;make room for 4 args
	TAX
	STY 3,X			;HiBytes n2,n3=0 n1 is 0
	STY 1,X
	BCS L314		;branch always

L313	INY			;skip 1. delimiter 
	BNE L314
	INC 5,X			;inc hibytes due to rollover
	INC 3,X
	INC 1,X
	INC N+3
L314	LDA (N+2),Y		;load CHR from adr1
	CMP N			;= c ?
	BEQ L313
L317	STY 4,X			; n2
L318	LDA (N+2),Y
	BNE L327
	STY 2,X			;$00 is absolute delimiter
	STY 0,X
	TYA
	CMP 4,X
	BNE L326
	INC 2,X
L326	JMP NEXT

L327	STY 2,X			;1. non c
	INY
	BNE L328
	INC 3,X			;inc hibytes
	INC 1,X
	INC N+3
L328	CMP N			;check for 2.delimiter
	BNE L318
	STY 0,X
	JMP NEXT


L337	DB $84,'EMI',$D4
	DW L301
EMIT	DW XEMIT


L344	DB $83,'KE',$D9
	DW L337
KEY	DW XKEY


L351	DB $89,'?TERMINA',$CC
	DW L344
QTERM	DW XQTER


L358	DB $82,'C',$D2
	DW L351
CR	DW XCR


L365	DB $85,'CMOV',$C5
	DW L358
CMOVE	DW CMOVE+2
	LDA #3
	JSR SETUP
L370	CPY N
	BNE L375
	DEC N+1
	BPL L375
	JMP NEXT
L375	LDA (N+4),Y
	STA (N+2),Y
	INY
	BNE L370
	INC N+5
	INC N+3
	JMP L370


L386	DB $82,'U',$AA	;U*
	DW L365
USTAR	DW USTAR+2
	TYA
	STA N
	LDY #16
	LSR 3,X
	ROR 2,X
L396	BCC L411
	CLC
	PHA
	LDA N
	ADC 0,X
 	STA N
	PLA
	ADC 1,X
L411	ROR A
	ROR N
	ROR 3,X
	ROR 2,X
	DEY
	BNE L396
	STA 1,X
	LDA N
	STA 0,X
	JMP NEXT


L418	DB $82,'U',$AF	;U/
	DW L386
USLAS	DW USLAS+2
	LDA 4,X
	LDY 2,X
	STY 4,X
	ASL A
	STA 2,X
	LDA 5,X
	LDY 3,X
	STY 5,X
	ROL A
	STA 3,X
	LDA #16
	STA N
L433	ROL 4,X
	ROL 5,X
	LDA 4,X
	BCS L446
	SEC
	SBC 0,X
	TAY
	LDA 5,X
	SBC 1,X
	BCC L444
	STY 4,X
L442	STA 5,X
L444	ROL 2,X
	ROL 3,X
	DEC N
	BNE L433
	JMP POP

L446	SBC 0,X
	STA 4,X
	LDA 5,X
	SBC 1,X
	SEC
	BCS L442

	;DS 4, $00		;alignment to avoid JMP($xxFF)

L453	DB $83,'AN',$C4
	DW L418
ANDD	DW ANDD+2
	LDA 0,X
	AND 2,X
	PHA
	LDA 1,X
	AND 3,X

BINARY	INX
	INX
	JMP PUT


L469	DB $82,'O',$D2
	DW L453
OR	DW OR+2
	LDA 0,X
	ORA 2,X
	PHA
	LDA 1,X
	ORA 3,X
	INX
	INX
	JMP PUT


L484	DB $83,'XO',$D2
	DW L469
XOR	DW XOR+2
	LDA 0,X
	EOR 2,X
	PHA
	LDA 1,X
	EOR 3,X
	INX
	INX
	JMP PUT


L499	DB $83,'SP',$C0
	DW L484
SPAT	DW SPAT+2
	TXA

PUSH0A	PHA
	LDA #0
	JMP PUSH


L511	DB $83,'SP',$A1
	DW L499
SPSTO	DW SPSTO+2
	LDY #6
	LDA (UP),Y
	TAX
	JMP NEXT


L522	DB $83,'RP',$A1
	DW L511
RPSTO	DW RPSTO+2
	STX XSAVE
	LDY #8
	LDA (UP),Y
	TAX
	TXS
	LDX XSAVE
	JMP NEXT


L536	DB $82,';',$D3
	DW L522
SEMIS	DW SEMIS+2
	PLA
	STA IP
	PLA
	STA IP+1
	JMP NEXT


L548	DB $85,'LEAV',$C5
	DW L536
LEAVE	DW LEAVE+2
	STX XSAVE
	TSX
	LDA $101,X
	STA $103,X
	LDA $102,X
	STA $104,X
	LDX XSAVE
	JMP NEXT


L563	DB $82,'>',$D2
	DW L548
TOR	DW TOR+2
	LDA 1,X
	PHA
	LDA 0,X
	PHA
	INX
	INX
	JMP NEXT


L577	DB $82,'R',$BE
	DW L563
RFROM	DW RFROM+2
	DEX
	DEX
	PLA
	STA 0,X
	PLA
	STA 1,X
	JMP NEXT


L591	DB $81,$D2
	DW L577
R	DW R+2
	STX XSAVE
	TSX
	LDA $101,X
	PHA
	LDA $102,X
	LDX XSAVE
	JMP PUSH


L605	DB $82,'0',$BD
	DW L591
ZEQU	DW ZEQU+2
	LDA 0,X
	ORA 1,X
	STY 1,X
	BNE L613
	INY
L613	STY 0,X
	JMP NEXT


L619	DB $82,'0',$BC
	DW L605
ZLESS	DW ZLESS+2
	ASL 1,X
	TYA
	ROL A
	STY 1,X
	STA 0,X
	JMP NEXT


L632	DB $81,$AB
	DW L619
PLUS	DW PLUS+2
	CLC
	LDA 0,X
	ADC 2,X
	STA 2,X
	LDA 1,X
	ADC 3,X
	STA 3,X
	INX
	INX
	JMP NEXT


L649	DB $82,'D',$AB
	DW L632
DPLUS	DW DPLUS+2
	CLC
	LDA 2,X
	ADC 6,X
	STA 6,X
	LDA 3,X
	ADC 7,X
	STA 7,X
	LDA 0,X
	ADC 4,X
	STA 4,X
	LDA 1,X
	ADC 5,X
	STA 5,X
	JMP POPTWO


L670	DB $85,'MINU',$D3
	DW L649
MINUS	DW MINUS+2
	SEC
	TYA
	SBC 0,X
	STA 0,X
	TYA
	SBC 1,X
	STA 1,X
	JMP NEXT


L685	DB $86,'DMINU',$D3
	DW L670
DMINU	DW DMINU+2
	SEC
	TYA
	SBC 2,X
	STA 2,X
	TYA
	SBC 3,X
	STA 3,X
	JMP MINUS+3


L700	DB $84,'OVE',$D2	;OVER
	DW L685
OVER	DW OVER+2
	LDA 2,X
	PHA
	LDA 3,X
	JMP PUSH


L711	DB $84,'DRO',$D0	;DROP
	DW L700
DROP	DW POP


L718	DB $84,'SWA',$D0	;SWAP
	DW L711
SWAP	DW SWAP+2
	LDA 2,X
	PHA
	LDA 0,X
	STA 2,X
	LDA 3,X
	LDY 1,X
	STY 3,X
	JMP PUT


L733	DB $83,'DU',$D0		;DUP
	DW L718
DUP	DW DUP+2
	LDA 0,X
	PHA
	LDA 1,X
	JMP PUSH


L744	DB $82,'+',$A1		;+!
	DW L733
PSTOR	DW PSTOR+2
	CLC
	LDA (0,X)
	ADC 2,X
	STA (0,X)
	INC 0,X
	BNE L754
	INC 1,X
L754	LDA (0,X)
	ADC 3,X
	STA (0,X)
	JMP POPTWO


L762	DB $86,'TOGGL',$C5
	DW L744
TOGGL	DW TOGGL+2
	LDA (2,X)
	EOR 0,X
	STA (2,X)
	JMP POPTWO


L773	DB $81,$C0		;@
	DW L762
AT	DW AT+2
	LDA (0,X)
	PHA
	INC 0,X
	BNE L781
	INC 1,X
L781	LDA (0,X)
	JMP PUT


L787	DB $82,'C',$C0		;C@
	DW L773
CAT	DW CAT+2
	LDA (0,X)
	STA 0,X
	STY 1,X
	JMP NEXT


L798	DB $81,$A1		;!
	DW L787
STORE	DW STORE+2
	LDA 2,X
	STA (0,X)
	INC 0,X
	BNE L806
	INC 1,X
L806	LDA 3,X
	STA (0,X)
	JMP POPTWO


L813	DB $82,'C',$A1		;C!
	DW L798
CSTOR	DW CSTOR+2
	LDA 2,X
	STA (0,X)
	JMP POPTWO


L823	DB $C1,$BA		;:
	DW L813
COLON	DW DOCOL,QEXEC,SCSP,CURR,AT
	DW CON,STORE,CREAT,RBRAC,PSCOD

DOCOL	LDA IP+1
	PHA
	LDA IP
	PHA
	CLC
	LDA W
	ADC #2
	STA IP
	TYA
	ADC W+1
	STA IP+1
	JMP NEXT


L853	DB $C1,$BB		;;
	DW L823
	DW DOCOL, QCSP, COMP, SEMIS, SMUDG
	DW LBRAC, SEMIS


L867	DB $88,'CONSTAN',$D4
	DW L853
CONST	DW DOCOL, CREAT, SMUDG, COMMA, PSCOD

DOCON	LDY #2
	LDA (W),Y
	PHA
	INY
	LDA (W),Y
	JMP PUSH


L885	DB $88,'VARIABL',$C5
	DW L867
VAR	DW DOCOL, CONST, PSCOD

DOVAR	CLC
	LDA W
	ADC #2
	PHA
	TYA
	ADC W+1
	JMP PUSH


L902	DB $84,'USE',$D2
	DW L885
USER	DW DOCOL, CONST, PSCOD

DOUSE	LDY #2
	CLC
	LDA (W),Y
	ADC UP
	PHA
	LDA #0
	ADC UP+1
	JMP PUSH


L920	DB $81,$B0
	DW L902
ZERO	DW DOCON, 0


L928	DB $81,$B1
	DW L920
ONE	DW DOCON, 1


L936	DB $81,$B2
	DW L928
TWO	DW DOCON, 2


L944	DB $81,$B3
	DW L936
THREE	DW DOCON, 3


L952	DB $82,'B',$CC
	DW L944
BL	DW DOCON, $20


L960	DB $83,'C/',$CC
	DW L952
CSLL	DW DOCON, 64


L968	DB $85,'FIRS',$D4	;FIRST
	DW L960
FIRST	DW DOCON, DAREA


L976	DB $85,'LIMI',$D4	;LIMIT
	DW L968
LIMIT	DW DOCON, UAREA


L978	DB $82,'L',$CF		;LO
	DW L976
LO	DW DOCON, LOWMEM


L980	DB $82,'H',$C9		;HI
	DW L978
HI	DW DOCON, HIMEM


L984	DB $85,'B/BU',$C6	;B/BUF
	DW L980
BBUF	DW DOCON,SSIZE


L992	DB $85,'B/SC',$D2	;B/SCR
	DW L984
BSCR	DW DOCON,NBUF


L1000	DB $87,'+ORIGI',$CE	;+ORIGIN
	DW L992
PORIG	DW DOCOL,LIT,ORIG
	DW PLUS,SEMIS


L1010	DB $83,'TI',$C2		;TIB
	DW L1000
TIB	DW DOUSE
	DB $A


L1018	DB $85,'WIDT',$C8	;WIDTH
	DW L1010
WIDTH	DW DOUSE
	DB $C


L1026	DB $87,'WARNIN',$C7	;WARNING
	DW L1018
WARN	DW DOUSE
	DB $E

L1034	DB $85,'FENC',$C5	;FENCE
	DW L1026
FENCE	DW DOUSE
	DB $10


L1042	DB $82,'D',$D0		;DP
	DW L1034
DP	DW DOUSE
	DB $12


L1050	DB $88,'VOC-LIN',$CB	;VOC-LINK
	DW L1042
VOCL	DW DOUSE
	DB $14


L1058	DB $83,'BL',$CB		;BLK
	DW L1050
BLK	DW DOUSE
	DB $16


L1066	DB $82,'I',$CE		;IN
	DW L1058
IN	DW DOUSE
	DB $18


L1074	DB $83,'OU',$D4		;OUT
	DW L1066
OUT	DW DOUSE
	DB $1A


L1082	DB $83,'SC',$D2		;SCR
	DW L1074
SCR	DW DOUSE
	DB $1C


L1090	DB $86,'OFFSE',$D4	;OFFSET
	DW L1082
OFSET	DW DOUSE
	DB $1E


L1098	DB $87,'CONTEX',$D4	;CONTEXT
	DW L1090
CON	DW DOUSE
	DB $20


L1106	DB $87,'CURREN',$D4	;CURRENT
	DW L1098
CURR	DW DOUSE
	DB $22


L1114	DB $85,'STAT',$C5	;STATE
	DW L1106
STATE	DW DOUSE
	DB $24


L1122	DB $84,'BAS',$C5	;BASE
	DW L1114
BASE	DW DOUSE
	DB $26


L1130	DB $83,'DP',$CC		;DPL
	DW L1122
DPL	DW DOUSE
	DB $28


L1138	DB $83,'FL',$C4		;FLD
	DW L1130
FLD	DW DOUSE
	DB $2A


L1146	DB $83,'CS',$D0		;CSP
	DW L1138
CSP	DW DOUSE
	DB $2C


L1154	DB $82,'R',$A3		;R#
	DW L1146
RNUM	DW DOUSE
	DB $2E


L1162	DB $83,'HL',$C4		;HLD
	DW L1154
HLD	DW DOUSE
	DB $30


L1164	DB $83,'EO',$CD		;EOM
	DW L1162
EOM	DW DOUSE
	DB $32


L1166	DB $87,'PRINTE','R'+$80	;PRINTER
	DW L1164
PRINTER	DW DOUSE
	DB $34


L1170	DB $82,'1',$AB		;1+
	DW L1166
ONEP	DW DOCOL,ONE,PLUS,SEMIS


L1180	DB $82,'2',$AB		;2+
	DW L1170
TWOP	DW DOCOL,TWO,PLUS,SEMIS


L1190	DB $84,'HER',$C5	;HERE
	DW L1180
HERE	DW DOCOL,DP,AT,SEMIS


L1200	DB $85,'ALLO',$D4	;ALLOT
	DW L1190
ALLOT	DW DOCOL,DP,PSTOR,SEMIS


L1210	DB $81,$AC		;,
	DW L1200
COMMA	DW DOCOL,HERE,STORE
	DW TWO,ALLOT,SEMIS


L1222	DB $82,'C',$AC		;C,
	DW L1210
CCOMM	DW DOCOL,HERE,CSTOR
	DW ONE,ALLOT,SEMIS


L1234	DB $81,$AD		;-
	DW L1222
SUB	DW DOCOL,MINUS,PLUS,SEMIS


L1244	DB $81,$BD		;=
	DW L1234
EQUAL	DW DOCOL,SUB,ZEQU,SEMIS


L1246	DB $82,'U',$BC		;U<
	DW L1244
ULESS	DW ULESS+2		;corrected
	LDA 3,X
	STY 3,X			;set Hibyte = 0
	CMP 1,X
	BNE L1248
	LDA 2,X
	CMP 0,X
L1248	BCS L1250
	INY			;true
L1250	STY 2,X
	JMP POP

L1254	DB $81,$BC		;<
	DW L1246
LESS	DW LESS+2
	SEC
	LDA 2,X
	SBC 0,X			;SUBTRACT
	LDA 3,X
	SBC 1,X
	STY 3,X			;HIBYTE=0
	BVC L1258
	EOR #$80		;CORRECT OVERFLOW
L1258	BPL L1260
	INY			;INVERT BOOLEAN
L1260	STY 2,X			;LEAVE BOOLEAN
	JMP POP

L1264	DB $81,$BE		;>
	DW L1254
GREAT	DW DOCOL,SWAP,LESS,SEMIS


L1274	DB $83,'RO',$D4		;ROT
	DW L1264
ROT	DW DOCOL,TOR,SWAP
	DW RFROM,SWAP,SEMIS


L1286	DB $85,'SPAC',$C5	;SPACE
	DW L1274
SPACE	DW DOCOL,BL,EMIT,SEMIS


L1296	DB $84,'-DU',$D0	;-DUP
	DW L1286
DDUP	DW DOCOL,DUP,ZBRAN
L1301	DW L1303-L1301,DUP
L1303	DW SEMIS


L1308	DB $88,'TRAVERS',$C5	;TRAVERSE
	DW L1296
TRAV	DW DOCOL,SWAP
L1312	DW OVER,PLUS,CLIT
	DB $7F
	DW OVER,CAT,LESS,ZBRAN
L1320	DW L1312-L1320,SWAP,DROP,SEMIS


L1328	DB $86,'LATES',$D4	;LATEST
	DW L1308
LATES	DW DOCOL,CURR,AT,AT,SEMIS


L1339	DB $83,'LF',$C1		;LFA
	DW L1328
LFA	DW DOCOL,CLIT
	DB 4
	DW SUB,SEMIS


L1350	DB $83,'CF',$C1		;CFA
	DW L1339
CFA	DW DOCOL,TWO,SUB,SEMIS


L1360	DB $83,'NF',$C1		;NFA
	DW L1350
NFA	DW DOCOL,CLIT
	DB 5
	DW SUB,LIT,$FFFF
	DW TRAV,SEMIS


L1373	DB $83,'PF',$C1		;PFA
	DW L1360
PFA	DW DOCOL,ONE,TRAV,CLIT
	DB 5
	DW PLUS,SEMIS


L1386	DB $84,'!CS',$D0	;!CSP
	DW L1373
SCSP	DW DOCOL,SPAT,CSP
	DW STORE,SEMIS


L1397	DB $86,'?ERRO',$D2	;?ERROR
	DW L1386
QERR	DW DOCOL,SWAP,ZBRAN
L1402	DW L1406-L1402,ERROR,BRAN
L1405	DW L1407-L1405
L1406	DW DROP
L1407	DW SEMIS


L1412	DB $85,'?COM',$D0	;?COMP
	DW L1397
QCOMP	DW DOCOL,STATE,AT
	DW ZEQU,CLIT
	DB $11
	DW QERR,SEMIS


L1426	DB $85,'?EXE',$C3	;?EXEC
	DW L1412
QEXEC	DW DOCOL,STATE,AT,CLIT
	DB $12
	DW QERR,SEMIS


L1439	DB $86,'?PAIR',$D3	;?PAIRS
	DW L1426
QPAIR	DW DOCOL,SUB,CLIT
	DB $13
	DW QERR,SEMIS


L1451	DB $84,'?CS',$D0	;?CSP
	DW L1439
QCSP	DW DOCOL,SPAT,CSP
	DW AT,SUB,CLIT
	DB $14
	DW QERR,SEMIS


L1466	DB $88,'?LOADIN',$C7	;?LOADING
	DW L1451
QLOAD	DW DOCOL,BLK,AT
	DW ZEQU,CLIT
	DB $16
	DW QERR,SEMIS


L1480	DB $87,'COMPIL',$C5	;COMPILE
	DW L1466
COMP	DW DOCOL,QCOMP,RFROM
	DW DUP,TWOP,TOR,AT
	DW COMMA,SEMIS


L1495	DB $C1,$DB		;\
	DW L1480
LBRAC	DW DOCOL,ZERO,STATE
	DW STORE,SEMIS


L1507	DB $81,$DD		;[
	DW L1495
RBRAC	DW DOCOL,CLIT
	DB $C0
	DW STATE,STORE,SEMIS


L1519	DB $86,'SMUDG',$C5	;SMUDGE
	DW L1507
SMUDG	DW DOCOL,LATES,CLIT
	DB $20
	DW TOGGL,SEMIS


L1531	DB $83,'HE',$D8		;HEX
	DW L1519
HEX	DW DOCOL,CLIT
	DB 16
	DW BASE,STORE,SEMIS


L1543	DB $87,'DECIMA',$CC	;DECIMAL
	DW L1531
DECIM	DW DOCOL,CLIT
	DB 10
	DW BASE,STORE,SEMIS


L1555	DB $87,'(;CODE',$A9	;(;CODE)
	DW L1543
PSCOD	DW DOCOL,RFROM,LATES,PFA
	DW CFA,STORE,SEMIS

L1568	DB $C5,';COD',$C5	; ;CODE
	DW L1555,DOCOL,QCSP,COMP
	DW PSCOD,LBRAC,SMUDG,SEMIS


L1582	DB $87,'<BUILD',$D3	;<BUILDS
	DW L1568
BUILD	DW DOCOL,ZERO,CONST,SEMIS


L1592	DB $85,'DOES',$BE	;DOES>
	DW L1582
DOES	DW DOCOL,RFROM,LATES
	DW PFA,STORE,PSCOD

DODOE	LDA IP+1
	PHA
	LDA IP
	PHA
	LDY #2
	LDA (W),Y
	STA IP
	INY
	LDA (W),Y
	STA IP+1
	CLC
	LDA W
	ADC #4
	PHA
	LDA W+1
	ADC #0
	JMP PUSH


L1622	DB $85,'COUN',$D4	;COUNT
	DW L1592
COUNT	DW DOCOL,DUP,ONEP
	DW SWAP,CAT,SEMIS


L1634	DB $84,'TYP',$C5	;TYPE
	DW L1622
TYPE	DW DOCOL,DDUP,ZBRAN
L1639	DW L1651-L1639
	DW OVER,PLUS,SWAP,PDO
L1644	DW I,CAT,EMIT,PLOOP
L1648	DW L1644-L1648,BRAN
L1650	DW L1652-L1650
L1651	DW DROP
L1652	DW SEMIS


L1657	DB $89,'-TRAILIN',$C7	;-TRAILING
	DW L1634
DTRAI	DW DOCOL,DUP,ZERO,PDO
L1663	DW OVER,OVER,PLUS,ONE
	DW SUB,CAT,BL,SUB,ZBRAN
L1672	DW L1676-L1672,LEAVE,BRAN
L1675	DW L1678-L1675
L1676	DW ONE,SUB
L1678	DW PLOOP
L1679	DW L1663-L1679,SEMIS


L1685	DB $84,'(."',$A9	;(.")
	DW L1657
PDOTQ	DW DOCOL,R,COUNT,DUP,ONEP
	DW RFROM,PLUS,TOR,TYPE,SEMIS


L1701	DB $C2,'.',$A2		;."
	DW L1685,DOCOL,CLIT
	DB $22
	DW STATE,AT,ZBRAN
L1709	DW L1719-L1709,COMP,PDOTQ,WORD
	DW HERE,CAT,ONEP,ALLOT,BRAN
L1718	DW L1723-L1718
L1719	DW WORD,HERE,COUNT,TYPE
L1723	DW SEMIS


L1729	DB $86,'EXPEC',$D4	;EXPECT
	DW L1701
EXPEC	DW DOCOL,OVER,PLUS,OVER,PDO
L1736	DW KEY,DUP,CLIT
	DB $E
	DW PORIG,AT,EQUAL,ZBRAN
L1744	DW L1760-L1744,DROP,CLIT
	DB 8
	DW OVER,I,EQUAL,DUP,RFROM
	DW TWO,SUB,PLUS,TOR,SUB,BRAN
L1759	DW L1779-L1759
L1760	DW DUP,CLIT
	DB $D
	DW EQUAL,ZBRAN
L1765	DW L1772-L1765,LEAVE,DROP
	DW BL,ZERO,BRAN
L1771	DW L1773-L1771
L1772	DW DUP
L1773	DW I,CSTOR,ZERO,I,ONEP,STORE
L1779	DW EMIT,PLOOP
L1781	DW L1736-L1781,DROP,SEMIS


L1788	DB $85,'QUER',$D9	;QUERY
	DW L1729
QUERY	DW DOCOL,TIB,AT,CLIT
	DB 80
	DW EXPEC,ZERO,IN,STORE,SEMIS


L1804	DB $C1,$80		; $00
	DW L1788,DOCOL,BLK,AT,ZBRAN
L1810	DW L1830-L1810,ONE,BLK,PSTOR
	DW ZERO,IN,STORE,BLK,AT
	DW ZERO,BSCR,USLAS,DROP
	DW ZEQU,ZBRAN
L1824	DW L1828-L1824,QEXEC,RFROM,DROP
L1828	DW BRAN
L1829	DW L1832-L1829
L1830	DW RFROM,DROP
L1832	DW SEMIS


L1838	DB $84,'FIL',$CC	;FILL
	DW L1804
FILL	DW DOCOL,SWAP,TOR,OVER
	DW CSTOR,DUP,ONEP,RFROM
	DW ONE,SUB,CMOVE,SEMIS


L1856	DB $85,'ERAS',$C5	;ERASE
	DW L1838
ERASE	DW DOCOL,ZERO,FILL,SEMIS


L1866	DB $86,'BLANK',$D3	;BLANKS
	DW L1856
BLANK	DW DOCOL,BL,FILL,SEMIS


L1876	DB $84,'HOL',$C4	;HOLD
	DW L1866
HOLD	DW DOCOL,LIT,$FFFF,HLD
	DW PSTOR,HLD,AT,CSTOR,SEMIS


L1890	DB $83,'PA',$C4		;PAD
	DW L1876
PAD	DW DOCOL,HERE,CLIT
	DB 68			;PAD IS 68 BYTES ABOVE HERE
	DW PLUS,SEMIS


L1902	DB $84,'WOR',$C4	;WORD
	DW L1890
WORD	DW DOCOL,BLK,AT,ZBRAN
L1908	DW L1914-L1908,BLK,AT
	DW BLOCK,BRAN
L1913	DW L1916-L1913
L1914	DW TIB,AT
L1916	DW IN,AT,PLUS,SWAP
	DW ENCL,HERE,CLIT
	DB $22
	DW BLANK,IN,PSTOR,OVER
	DW SUB,TOR,R,HERE,CSTOR
	DW PLUS,HERE,ONEP,RFROM,CMOVE
	DW SEMIS


L1943	DB $85,'UPPE',$D2	;UPPER
	DW L1902
UPPER	DW DOCOL,OVER,PLUS,SWAP,PDO
L1950	DW I,CAT,CLIT
	DB $5F
	DW GREAT,ZBRAN
L1956	DW L1961-L1956,I,CLIT
	DB $20
	DW TOGGL
L1961	DW PLOOP
L1962	DW L1950-L1962,SEMIS


L1968	DB $88,'(NUMBER',$A9	;(NUMBER)
	DW L1943
PNUMB	DW DOCOL
L1971	DW ONEP,DUP,TOR,CAT
	DW BASE,AT,DIGIT,ZBRAN
L1979	DW L2001-L1979,SWAP,BASE
	DW AT,USTAR,DROP,ROT,BASE
	DW AT,USTAR,DPLUS,DPL
	DW AT,ONEP,ZBRAN
L1994	DW L1998-L1994,ONE,DPL,PSTOR
L1998	DW RFROM,BRAN
L2000	DW L1971-L2000
L2001	DW RFROM,SEMIS


L2007	DB $86,'NUMBE',$D2	;NUMBER
	DW L1968
NUMBER	DW DOCOL,ZERO,ZERO,ROT
	DW DUP,ONEP,CAT,CLIT
	DB $2D
	DW EQUAL,DUP,TOR,PLUS
	DW LIT,$FFFF
L2023	DW DPL,STORE,PNUMB,DUP
	DW CAT,BL,SUB,ZBRAN
L2031	DW L2042-L2031,DUP,CAT
	DW CLIT
	DB $2E
	DW SUB,ZERO,QERR,ZERO,BRAN
L2041	DW L2023-L2041
L2042	DW DROP,RFROM,ZBRAN
L2045	DW L2047-L2045,DMINU
L2047	DW SEMIS


L2052	DB $85,'-FIN',$C4	;-FIND
	DW L2007
DFIND	DW DOCOL,BL,WORD,HERE
	DW COUNT,UPPER		;CASE NEGLECT
	DW HERE,CON,AT,AT,PFIND
	DW DUP,ZEQU,ZBRAN
L2068	DW L2073-L2068,DROP,HERE
	DW LATES,PFIND
L2073	DW SEMIS


L2078	DB $87,'(ABORT',$A9	;(ABORT)
	DW L2052
PABOR	DW DOCOL,ABORT,SEMIS


L2087	DB $85,'ERRO',$D2	;ERROR
	DW L2078
ERROR	DW DOCOL,WARN,AT
	DW ZLESS,ZBRAN
L2094	DW L2096-L2094,PABOR
L2096	DW HERE,COUNT,TYPE,PDOTQ
	DB 4,'  ? '
	DW MESS,SPSTO,DROP,DROP	;ROOM FOR 2 ERROR VALUES
	DW IN,AT,BLK,AT,QUIT
	DW SEMIS


L2113	DB $83,'ID',$AE		;ID.
	DW L2087
IDDOT	DW DOCOL,PAD,CLIT
	DB $20
	DW CLIT
	DB $5F
	DW FILL,DUP,PFA,LFA
	DW OVER,SUB,PAD,SWAP
	DW CMOVE,PAD,COUNT,CLIT
	DB $1F
	DW ANDD,TYPE,SPACE,SEMIS


L2142	DB $86,'CREAT',$C5	;CREATE
	DW L2113
CREAT	DW DOCOL,TIB,HERE,CLIT
	DB $A0			;CHECKS FOR ROOM IN DICTIONARY
	DW PLUS,ULESS,TWO
	DW QERR,DFIND,ZBRAN
L2155	DW L2163-L2155,DROP,NFA
	DW IDDOT,CLIT
	DB 4
	DW MESS,SPACE
L2163	DW HERE,DUP,CAT,WIDTH
	DW AT,MIN,ONEP,ALLOT
	DW DP,CAT,CLIT
	DB $FD			;CHECK FOR JMP ($xxFF)
	DW EQUAL,ALLOT,DUP,CLIT
	DB $A0
	DW TOGGL,HERE,ONE
	DW SUB,CLIT
	DB $80
	DW TOGGL,LATES,COMMA,CURR
	DW AT,STORE,HERE
	DW TWOP,COMMA,SEMIS


L2200	DB $C9,$5B,'COMPILE',$DD	;[COMPILE]
	DW L2142,DOCOL,DFIND
	DW ZEQU,ZERO,QERR,DROP
	DW CFA,COMMA,SEMIS


L2216	DB $C7,'LITERA',$CC	;LITERAL
	DW L2200
LITER	DW DOCOL,STATE,AT,ZBRAN
L2222	DW L2226-L2222,COMP
	DW LIT,COMMA
L2226	DW SEMIS


L2232	DB $C8,'DLITERA',$CC	;DLITERAL
	DW L2216
DLIT	DW DOCOL,STATE,AT,ZBRAN
L2238	DW L2242-L2238,SWAP
	DW LITER,LITER
L2242	DW SEMIS


L2248	DB $86,'?STAC',$CB	;?STACK
	DW L2232
QSTAC	DW DOCOL,SPAT,CLIT
	DB TOS
	DW SWAP,ULESS,ONE,QERR
	DW SPAT,CLIT
	DB BOS
	DW ULESS,CLIT
	DB 7
	DW QERR,SEMIS


L2269	DB $89,'INTERPRE',$D4	;INTERPRET
	DW L2248
INTER	DW DOCOL
L2272	DW DFIND,ZBRAN
L2274	DW L2289-L2274,STATE
	DW AT,LESS,ZBRAN
L2279	DW L2284-L2279
	DW CFA,COMMA,BRAN
L2283	DW L2286-L2283
L2284	DW CFA,EXEC
L2286	DW QSTAC,BRAN
L2288	DW L2302-L2288
L2289	DW HERE,NUMBER,DPL
	DW AT,ONEP,ZBRAN
L2295	DW L2299-L2295,DLIT,BRAN
L2298	DW L2301-L2298
L2299	DW DROP,LITER
L2301	DW QSTAC
L2302	DW BRAN
L2303	DW L2272-L2303


L2309	DB $89,'IMMEDIAT',$C5	;IMMEDIATE
	DW L2269,DOCOL,LATES,CLIT
	DB $40
	DW TOGGL,SEMIS


L2321	DB $8A,'VOCABULAR',$D9	;VOCABULARY
	DW L2309,DOCOL,BUILD,LIT
	DW $A081,COMMA,CURR,AT
	DW CFA,COMMA,HERE,VOCL
	DW AT,COMMA,VOCL,STORE
	DW DOES
DOVOC	DW TWOP,CON,STORE,SEMIS


L2346	DB $C5,'FORT',$C8	;FORTH
	DW L2321
FORTH	DW DODOE,DOVOC,$A081
XFOR	DW NTOP
VLO	DW 0


L2357	DB $8B,'DEFINITION',$D3	;DEFINITIONS
	DW L2346
DEFIN	DW DOCOL,CON,AT,CURR
	DW STORE,SEMIS


L2369	DB $C1,$A8		;(
	DW L2357,DOCOL,CLIT
	DB ')'
	DW WORD,SEMIS


L2381	DB $84,'QUI',$D4	;QUIT
	DW L2369
QUIT	DW DOCOL,ZERO,BLK
	DW STORE,LBRAC
L2388	DW RPSTO,CR,QUERY,INTER
	DW STATE,AT,ZEQU,ZBRAN
L2396	DW L2399-L2396,PDOTQ
	DB 2,'ok'
L2399	DW BRAN
L2400	DW L2388-L2400,SEMIS


L2406	DB $85,'ABOR',$D4	;ABORT
	DW L2381
ABORT	DW DOCOL,SPSTO,DECIM,EMPBUF
	DW ZERO,OFSET,STORE	;set OFFSET=0
	DW ZERO,PRINTER,STORE	;PRINTER=0
	DW LIT,LOWMEM,EOM,STORE	;initialize EOM
	DW CR,PDOTQ
	DB 15,' fig-FORTH  ',VERSION/16+$30,'.',VERSION*$1000/$1000+$30
	DW FORTH,DEFIN,QUIT


L2423	DB $84,'COL',$C4	;COLD
	DW L2406
COLD	DW COLD+2
	LDA INBUFV		;set TIBX
	STA ORIG+$16
	LDA INBUFV+1
	STA ORIG+$17
	LDA ORIG+$0C
	STA FORTH+6
	LDA ORIG+$0D
	STA FORTH+7
	LDY #$15
	BNE L2433
WARM	LDY #$0F
L2433	LDA ORIG+$10
	STA UP
	LDA ORIG+$11
	STA UP+1
L2437	LDA ORIG+$0C,Y
	STA (UP),Y
	DEY
	BPL L2437
	LDA #ABORT+2/256
	STA IP+1
	LDA #ABORT+2
	STA IP
	CLD
	LDA #$6C
	STA W-1
	JMP RPSTO+2		;AND OFF WE GO!


L2453	DB $84,'S->',$C4	;S->D
	DW L2423
STOD	DW DOCOL,DUP,ZLESS
	DW MINUS,SEMIS


L2464	DB $82,'+',$AD		;+-
	DW L2453
PM	DW DOCOL,ZLESS,ZBRAN
L2469	DW L2471-L2469,MINUS
L2471	DW SEMIS


L2476	DB $83,'D+',$AD		;D+-
	DW L2464
DPM	DW DOCOL,ZLESS,ZBRAN
L2481	DW L2483-L2481,DMINU
L2483	DW SEMIS


L2488	DB $83,'AB',$D3		;ABS
	DW L2476
ABS	DW DOCOL,DUP,PM,SEMIS


L2498	DB $84,'DAB',$D3	;DABS
	DW L2488
DABS	DW DOCOL,DUP,DPM,SEMIS


L2508	DB $83,'MI',$CE		;MIN
	DW L2498
MIN	DW DOCOL,OVER,OVER
	DW GREAT,ZBRAN
L2515	DW L2517-L2515,SWAP
L2517	DW DROP,SEMIS


L2523	DB $83,'MA',$D8		;MAX
	DW L2508
MAX	DW DOCOL,OVER,OVER
	DW LESS,ZBRAN
L2530	DW L2532-L2530,SWAP
L2532	DW DROP,SEMIS


L2538	DB $82,'M',$AA		;M*
	DW L2523
MSTAR	DW DOCOL,OVER,OVER,XOR
	DW TOR,ABS,SWAP,ABS
	DW USTAR,RFROM,DPM,SEMIS


L2556	DB $82,'M',$AF		;M/
	DW L2538
MSLAS	DW DOCOL,OVER,TOR,TOR
	DW DABS,R,ABS,USLAS
	DW RFROM,R,XOR,PM,SWAP
	DW RFROM,PM,SWAP,SEMIS


L2579	DB $81,$AA		;*
	DW L2556
STAR	DW DOCOL,USTAR,DROP,SEMIS


L2589	DB $84,'/MO',$C4	;/MOD
	DW L2579
SLMOD	DW DOCOL,TOR,STOD
	DW RFROM,MSLAS,SEMIS


L2601	DB $81,$AF		;/
	DW L2589
SLASH	DW DOCOL,SLMOD,SWAP
	DW DROP,SEMIS


L2612	DB $83,'MO',$C4		;MOD
	DW L2601
MOD	DW DOCOL,SLMOD,DROP,SEMIS


L2622	DB $85,'*/MO',$C4	;*/MOD
	DW L2612
SSMOD	DW DOCOL,TOR,MSTAR
	DW RFROM,MSLAS,SEMIS


L2634	DB $82,'*',$AF		;*/
	DW L2622
SSLAS	DW DOCOL,SSMOD,SWAP
	DW DROP,SEMIS


L2645	DB $85,'M/MO',$C4	;M/MOD
	DW L2634
MSMOD	DW DOCOL,TOR,ZERO,R
	DW USLAS,RFROM,SWAP
	DW TOR,USLAS,RFROM,SEMIS


L2662	DB $83,'US',$C5		;USE
	DW L2645
USE	DW DOVAR,DAREA


L2670	DB $84,'PRE',$D6	;PREV
	DW L2662
PREV	DW DOVAR,DAREA


L2678	DB $84,'+BU',$C6	;+BUF
	DW L2670
PBUF	DW DOCOL,LIT,SSIZE+4
	DW PLUS,DUP,LIMIT
	DW EQUAL,ZBRAN
L2688	DW L2691-L2688,DROP,FIRST
L2691	DW DUP,PREV,AT,SUB,SEMIS


L2700	DB $86,'UPDAT',$C5	;UPDATE
	DW L2678
UPDAT	DW DOCOL,PREV,AT,AT
	DW LIT,$8000,OR,PREV
	DW AT,STORE,SEMIS

L2705	DB $85,'FLUS',$C8	;FLUSH
	DW L2700,DOCOL,LIMIT
	DW FIRST,SUB,BBUF,CLIT
	DB 4
	DW PLUS,SLASH,ONEP
	DW ZERO,PDO
L2835	DW LIT,$7FFF,BUFFR
	DW DROP,PLOOP
L2839	DW L2835-L2839,SEMIS


L2716	DB $8D,'EMPTY-BUFFER',$D3	;EMPTY-BUFFERS
	DW L2705
EMPBUF	DW DOCOL,FIRST,LIMIT
	DW OVER,SUB,ERASE,SEMIS


L2751	DB $86,'BUFFE',$D2	;BUFFER
	DW L2716
BUFFR	DW DOCOL,USE,AT,DUP
	DW TOR
L2758	DW PBUF,ZBRAN
L2760	DW L2758-L2760,USE,STORE
	DW R,AT,ZLESS,ZBRAN
L2767	DW L2776-L2767,R,TWOP
	DW R,AT,LIT,$7FFF
	DW ANDD,ZERO,RSLW
L2776	DW R,STORE,R,PREV,STORE
	DW RFROM,TWOP,SEMIS


L2788	DB $85,'BLOC',$CB	;BLOCK
	DW L2751
BLOCK	DW DOCOL,OFSET,AT,PLUS
	DW TOR,PREV,AT,DUP,AT
	DW R,SUB,DUP,PLUS,ZBRAN
L2804	DW L2830-L2804
L2805	DW PBUF,ZEQU,ZBRAN
L2808	DW L2818-L2808,DROP,R
	DW BUFFR,DUP,R,ONE
	DW RSLW,TWO,SUB
L2818	DW DUP,AT,R,SUB,DUP
	DW PLUS,ZEQU,ZBRAN
L2826	DW L2805-L2826,DUP
	DW PREV,STORE
L2830	DW RFROM,DROP,TWOP,SEMIS


L2838	DB $86,'(LINE',$A9	;(LINE)
	DW L2788
PLINE	DW DOCOL,TOR,CSLL,BBUF
	DW SSMOD,RFROM,BSCR,STAR
	DW PLUS,BLOCK,PLUS,CSLL
	DW SEMIS


L2857	DB $85,'.LIN',$C5	;.LINE
	DW L2838
DLINE	DW DOCOL,PLINE,DTRAI
	DW TYPE,SEMIS


L2868	DB $87,'MESSAG',$C5	;MESSAGE
	DW L2857
MESS	DW DOCOL,WARN,AT,ZBRAN
L2874	DW L2888-L2874,DDUP,ZBRAN
L2877	DW L2886-L2877,CLIT
	DB 4
	DW OFSET,AT,BSCR,SLASH
	DW SUB,DLINE
L2886	DW BRAN
L2887	DW L2891-L2887
L2888	DW PDOTQ
	DB 6,'MSG # '
	DW DOT
L2891	DW SEMIS


L2896	DB $84,'LOA',$C4	;LOAD
	DW L2868
LOAD	DW DOCOL,BLK,AT,TOR
	DW IN,AT,TOR,ZERO
	DW IN,STORE,BSCR,STAR
	DW BLK,STORE,INTER,RFROM
	DW IN,STORE,RFROM,BLK
	DW STORE,SEMIS


L2924	DB $C3,'--',$BE		;-->
	DW L2896
	DW DOCOL,QLOAD,ZERO
	DW IN,STORE,BSCR,BLK
	DW AT,OVER,MOD,SUB
	DW BLK,PSTOR,SEMIS


XEMIT	TYA			;WRITE ASCII TO SCREEN
	SEC
	LDY #$1A
	ADC (UP),Y
	STA (UP),Y
	INY			;BUMP USER VARIABLE OUT
	LDA #0
	ADC (UP),Y
	STA (UP),Y
	LDA 0,X			;FETCH CHR TO OUTPUT
	JSR PRTCHR		;AND DISPLAY IT
	JMP POP


XQTER	LDY #2			;GET CONSOLE STATUS
;	STX XSAVE
	JSR BIOS		;0 IF NO KEY
	CMP #0
	BEQ XQTER1
	LDY #3			;else read key = $03
	JSR BIOS
	LDA #1			;and forget it
XQTER1	JMP PUSH0A


XKEY	LDY #3			;READS 1 KEY TO STACK
;	STX XSAVE
	JSR BIOS		;VIA BIOS CONIN
	AND #$7F		;ASCII only
	JMP PUSH0A


XCR	JSR CRLF		;PRINTS A CR + LF
	JMP NEXT


CRLF	LDA #$0D
	JSR PRTCHR
	LDA #$0A

PRTCHR	STX XSAVE
	PHA
	LDY #$34
	LDA (UP),Y		;check PRINTER
	BEQ PRTCHR1
	PLA
	PHA
	LDX #BD_PRT		;Print CHR via BDOS
	JSR BDOSJP
PRTCHR1 PLA
	AND #$7F
	LDY #4			;direct console I/O
	BNE BIOS1


BIOS	STX XSAVE		;BIOS CALL
BIOS1	LDX #$1A		;QUICK AND DIRTY
	JSR BDOSJP
	LDX XSAVE
	RTS


L3060	DB $83,'R/',$D7		;R/W 1 SECTOR in RAMdisc
	DW L2924
RSLW	DW DOCOL
	DW TOR,LIT,SSIZE,STAR
	DW LIT,LOWMEM-$C00,PLUS ;SCR #3 is first screen
	DW DUP,LIT,LOWMEM,ULESS	;check LOWMEM and HIMEM
	DW OVER,LIT,HIMEM
	DW GREAT,OR,CLIT
	DB 6
	DW QERR
	DW DUP,EOM,AT		;check EOM
	DW GREAT,ZBRAN
L3063	DW L3064-L3063
	DW DUP,EOM,STORE	;set EOM
L3064	DW RFROM,ZBRAN
L3061	DW L3062-L3061,SWAP
L3062	DW LIT,SSIZE,CMOVE
	DW SEMIS


L3202	DB $C1,$A7		;'
	DW L3060
TICK	DW DOCOL,DFIND,ZEQU,ZERO
	DW QERR,DROP,LITER,SEMIS


L3217	DB $86,'FORGE',$D4	;FORGET
	DW L3202
FORG	DW DOCOL,TICK,NFA,DUP
	DW FENCE,AT,ULESS,CLIT
	DB $15
	DW QERR,TOR,VOCL,AT
L3220	DW R,OVER,ULESS,ZBRAN
L3222	DW L3225-L3222
	DW FORTH,DEFIN,AT,DUP
	DW VOCL,STORE,BRAN
L3224	DW L3220-L3224
L3225	DW DUP,CLIT
	DB 4
	DW SUB
L3228	DW PFA,LFA,AT,DUP,R
	DW ULESS,ZBRAN
L3230	DW L3228-L3230
	DW OVER,TWO,SUB,STORE
	DW AT,DDUP,ZEQU,ZBRAN
L3240	DW L3225-L3240
	DW RFROM,DP,STORE,SEMIS


L3250	DB $84,'BAC',$CB	;BACK
	DW L3217
BACK	DW DOCOL,HERE,SUB,COMMA
	DW SEMIS


L3261	DB $C5,'BEGI',$CE	;BEGIN
	DW L3250
	DW DOCOL,QCOMP,HERE
	DW ONE,SEMIS


L3273	DB $C5,'ENDI',$C6	;ENDIF
	DW L3261
ENDIF	DW DOCOL,QCOMP,TWO
	DW QPAIR,HERE,OVER
	DW SUB,SWAP
	DW STORE,SEMIS


L3290	DB $C4,'THE',$CE	;THEN
	DW L3273
	DW DOCOL,ENDIF,SEMIS


L3300	DB $C2,'D',$CF		;DO
	DW L3290
	DW DOCOL,COMP,PDO
	DW HERE,THREE,SEMIS


L3313	DB $C4,'LOO',$D0	;LOOP
	DW L3300
	DW DOCOL,THREE,QPAIR
	DW COMP,PLOOP,BACK,SEMIS


L3327	DB $C5,'+LOO',$D0	;+LOOP
	DW L3313
	DW DOCOL,THREE,QPAIR
	DW COMP,PPLOO
	DW BACK,SEMIS


L3341	DB $C5,'UNTI',$CC	;UNTIL
	DW L3327
UNTIL	DW DOCOL,ONE,QPAIR
	DW COMP,ZBRAN
	DW BACK,SEMIS


L3355	DB $C3,'EN',$C4		;END
	DW L3341
	DW DOCOL,UNTIL,SEMIS


L3365	DB $C5,'AGAI',$CE	;AGAIN
	DW L3355
AGAIN	DW DOCOL,ONE,QPAIR
	DW COMP,BRAN
	DW BACK,SEMIS


L3379	DB $C6,'REPEA',$D4	;REPEAT
	DW L3365
	DW DOCOL,TOR,TOR
	DW AGAIN,RFROM,RFROM
	DW TWO,SUB,ENDIF,SEMIS


L3396	DB $C2,'I',$C6		;IF
	DW L3379
IF	DW DOCOL,COMP,ZBRAN
	DW HERE,ZERO,COMMA
	DW TWO,SEMIS


L3411	DB $C4,'ELS',$C5	;ELSE
	DW L3396
	DW DOCOL,TWO,QPAIR
	DW COMP,BRAN,HERE,ZERO
	DW COMMA,SWAP,TWO
	DW ENDIF,TWO,SEMIS


L3431	DB $C5,'WHIL',$C5	;WHILE
	DW L3411
	DW DOCOL,IF,TWOP,SEMIS


L3442	DB $86,'SPACE',$D3	;SPACES
	DW L3431
SPACS	DW DOCOL,ZERO,MAX
	DW DDUP,ZBRAN
L3449	DW L3455-L3449
	DW ZERO,PDO
L3452	DW SPACE,PLOOP
L3454	DW L3452-L3454
L3455	DW SEMIS


L3460	DB $82,'<',$A3		;<#
	DW L3442
BDIGS	DW DOCOL,PAD,HLD
	DW STORE,SEMIS


L3471	DB $82,'#',$BE		;#>
	DW L3460
EDIGS	DW DOCOL,DROP,DROP
	DW HLD,AT,PAD,OVER
	DW SUB,SEMIS


L3486	DB $84,'SIG',$CE	;SIGN
	DW L3471
SIGN	DW DOCOL,ROT,ZLESS
	DW ZBRAN
L3492	DW L3496-L3492,CLIT
	DB $2D
	DW HOLD
L3496	DW SEMIS


L3501	DB $81,$A3		;#
	DW L3486
DIG	DW DOCOL,BASE,AT
	DW MSMOD,ROT,CLIT
	DB 9
	DW OVER,LESS,ZBRAN
L3513	DW L3517-L3513,CLIT
	DB 7
	DW PLUS
L3517	DW CLIT
	DB $30
	DW PLUS,HOLD,SEMIS


L3526	DB $82,'#',$D3		;#S
	DW L3501
DIGS	DW DOCOL
L3529	DW DIG,OVER,OVER,OR
	DW ZEQU,ZBRAN
L3535	DW L3529-L3535
	DW SEMIS


L3541	DB $83,'D.',$D2		;D.R
	DW L3526
DDOTR	DW DOCOL,TOR,SWAP
	DW OVER,DABS,BDIGS
	DW DIGS,SIGN,EDIGS
	DW RFROM,OVER,SUB
	DW SPACS,TYPE,SEMIS


L3562	DB $82,'D',$AE		;D.
	DW L3541
DDOT	DW DOCOL,ZERO,DDOTR
	DW SPACE,SEMIS


L3573	DB $82,'.',$D2		;.R
	DW L3562
DOTR	DW DOCOL,TOR,STOD
	DW RFROM,DDOTR,SEMIS


L3585	DB $81,$AE		;.
	DW L3573
DOT	DW DOCOL,STOD,DDOT,SEMIS


L3595	DB $81,$BF		;?
	DW L3585
QUES	DW DOCOL,AT,DOT,SEMIS


L3605	DB $84,'LIS',$D4	;LIST
	DW L3595
LIST	DW DOCOL,DECIM,CR,DUP
	DW SCR,STORE,PDOTQ
	DB 6,'SCR # '
	DW DOT,CLIT
	DB 16
	DW ZERO,PDO
L3620	DW CR,I,THREE,DOTR
	DW SPACE,I,SCR,AT
	DW DLINE,PLOOP
L3630	DW L3620-L3630
	DW CR,SEMIS


L3637	DB $85,'INDE',$D8	;INDEX
	DW L3605
	DW DOCOL,CR,ONEP,SWAP
	DW PDO
L3647	DW CR,I,THREE,DOTR
	DW SPACE,ZERO,I,DLINE
	DW QTERM,ZBRAN
L3657	DW L3659-L3657,LEAVE
L3659	DW PLOOP
L3660	DW L3647-L3660,CLIT
	DB $C			;FORM FEED
	DW EMIT,SEMIS


L3666	DB $85,'TRIA',$C4	;TRIAD
	DW L3637
	DW DOCOL,THREE,SLASH
	DW THREE,STAR,THREE
	DW OVER,PLUS,SWAP,PDO
L3681	DW CR,I,LIST,PLOOP
L3685	DW L3681-L3685
	DW CR,CLIT
	DB $F
	DW MESS,CR,CLIT
	DB $C			;FORM FEED
	DW EMIT,SEMIS


L3696	DB $85,'VLIS',$D4	;VLIST
	DW L3666
VLIST	DW DOCOL,CLIT
	DB $80
	DW OUT,STORE,CON,AT
	DW AT
L3706	DW OUT,AT,CSLL
	DW GREAT,ZBRAN
L3711	DW L3716-L3711
	DW CR,ZERO,OUT,STORE
L3716	DW DUP,IDDOT,SPACE
	DW SPACE,PFA,LFA
	;DW DUP,DOT		;only if JMP ($xxFF) is suspected
	DW AT,DUP,ZEQU,QTERM
	DW OR,ZBRAN
L3728	DW L3706-L3728
	DW DROP,SEMIS


L4000	DB $84,'BDO',$D3	;BDOS   A/Y X --> A/Y C
	DW L3696
BDOS	DW BDOS+2
	LDA 2,X			;GET A
	PHA
	LDY 3,X			;GET Y
	LDA 0,X			;GET X
	STX XSAVE
	TAX
	PLA
	JSR BDOSJP
	LDX XSAVE
	STA 2,X			;SAVE A
	STY 3,X			;SAVE Y
	LDY #0
	BCC L4001
	STY 3,X			;$00xx   xx is errorcode
	INY
L4001	STY 0,X			;SAVE C
	JMP NEXT


NTOP	DB $83,'MO',$CE		;MON
	DW L4000
MON	DW MON+2
	LDY #16			;MONITOR CALL
	JSR BIOS
	JMP NEXT


TOP	END
