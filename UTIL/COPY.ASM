;COPY.ASM
;(c) by D. Lausberg  1995

;Kopierprogramm fr 1 und 2 Disk Drives
;Aufruf   COPY d:filename.ext d: /switch
;Switches   Y   Y/N Abfrage nach jedem Filenamen

;V1.0	20.03.95
;V1.1	31.03.95	no disc change on C:
;V1.2	31.12.95	no disc change on C: and above
;V1.3	11.04.02	sector BIOS version
;V1.4	30.04.21	4 Floppies

;Parameters

DIRPNT	= $0		;DIR POINTER
DIRPNT1	= $1		;AUXILIARY DIR POINTER
CNT	= $2		;COUNTER
SDRIVE	= $3		;SOURCE DRIVE FOR SELDSK
SD_ASC	= $4		;DRIVE CHARACTER
DDRIVE	= $5		;DESTINATION DRIVE
DD_ASC	= $6
F_CNT	= $7		;FILE COUNTER
1PAGE	= $8		;1. PAGE OF HEAP
LPAGE	= $9		;LAST PAGE OF HEAP
S_CNT	= $A		;SOURCE COUNTER
D_CNT	= $B		;DESTINATION COUNTER
FLAG	= $C
SWITCH	= $D
ERRNO	= $E
PNT	= $F
PAGES	= $10		;nr of pages to buffer files

BUFPNT	= $20		;FILENAME BUFFER POINTER
S_PNT	= $22		;SOURCE FILENAME BUFFER POINTER

CCPV	= $DE
BDOS	= $F0
FCB1	= $F6
FCB2	= $F4
DIRBUF	= $FC
DMA	= $FE

TPA	= $200

;CONSTANTS

DIR_EXT	= $0C

;ERROR CODES

ABORT	= $81
NOSWITCH = $82
NOFILENAME = $83
NO_FILE	= $84
FILE_TOO_LONG = $85
READY	= $9E
NOCOPY	= $9F

EOF	= $D7
DOUBLE	= $DC
NOTFND	= $DD

;BDOS COMMANDS

CONIN	= $1
CONOUT	= $2
STROUT	= $9
SELDRV	= $E
OPEN	= $F
CLOSE	= $10
FIRST	= $11
NEXT	= $12
RDSEQ	= $14
WDSEQ	= $15
CREATE	= $16
GETDRV	= $19
BIOSC	= $1A

;BIOS COMMANDS

_CHRIN	= 3

;CONTROL CHARACTERS

CR	= $D
LF	= $A
EOT	= $0
SP	= $20

;--------------------------------------------------------

	ORG TPA


COPY	LDA #STARTM		;DISPLAY STARTUP MESSAGE
	LDY #STARTM/256
	JSR PRTSTR
	JSR INIT		;INITIALIZE
	BCS COPYX
	JSR GETFIL
	BCS COPYX
	LDY BUFPNT+1
	INY
	STY 1PAGE		;SET 1. PAGE OF HEAP
	LDA BDOS+2
	STA LPAGE		;SET LAST PAGE
	SEC
	SBC 1PAGE
	STA PAGES		;set nr. of pages
	JSR RESP		;RESET BUFFER POINTERS
COPY1	JSR RDFIL		;READ FILES
	BCS COPYX
	JSR WRFIL
	BCC COPY1
	CMP #READY
	BEQ COPY2
COPYX	JSR ERROR
COPY2	LDA #ENDM
	LDY #ENDM/256
	JSR PRTSTR
	LDA SDRIVE
	LDX #SELDRV
	JSR BDOS
	LDX #0
	JMP BDOS

;-----  SUBROUTINES  ----------

RDFIL	JSR RESDMA		;READ FILES TO COPY TILL EOM
	JSR CH_SD		;GET SOURCE DISK
	BCS RDFILX
RDFIL1	LDA #READING
	LDY #READING/256
	JSR PRTSTR
RDFIL8	JSR SETSFCB		;SET FCB WITH SOURCE FILENAME
	JSR PRTFCB		;print filename
RDFIL3	LDX #OPEN		;OPEN FILE
	JSR BDOS
	BCS RDFIL2
	LDA #0
	STA CNT
	LDA DMA+1
	LDY #14			;SAVE START PAGE
	STA (S_PNT),Y
RDFIL5	LDX #RDSEQ		;READ 1 BLOCK
	JSR BDOS
	BCS RDFIL4
	INC DMA+1
	INC CNT
	LDA DMA+1
	CMP LPAGE
	BCC RDFIL5
	LDA CNT
	CMP PAGES
	BCS RDFIL9
	RTS

RDFIL4	CMP #EOF
	BEQ RDFIL6
	JSR ERROR
	BCC RDFIL5
	RTS

RDFIL6	LDY #12
	LDA CNT
	STA (S_PNT),Y
	INC S_CNT
	LDA S_CNT
	CMP F_CNT
	BEQ RDFIL7
	CLC
	LDA #16			;SET POINTER TO NEXT FILE
	ADC S_PNT
	STA S_PNT
	LDA #0
	ADC S_PNT+1
	STA S_PNT+1
	JMP RDFIL8	

RDFIL2	JSR ERROR
	BCC RDFIL3
RDFILX	RTS

RDFIL7	CLC
	RTS

RDFIL9	LDA #FILE_TOO_LONG
	RTS


WRFIL	JSR CH_DD		;WRITE FILES TO DISK
	BCS WRFILX
	LDA #WRITING
	LDY #WRITING/256
	JSR PRTSTR
WRFIL6	JSR SETDFCB		;SET DESTINATION FCB1
	JSR PRTFCB
	JSR MAKFIL		;MAKE FILE
	BCS WRFIL1
	LDY #12
	LDA (BUFPNT),Y
	STA CNT
	LDY #14
	LDA (BUFPNT),Y
	STA DMA+1
WRFIL3	LDX #WDSEQ
	JSR BDOS
	BCS WRFIL2
	INC DMA+1
	DEC CNT
	BNE WRFIL3
	LDX #CLOSE
	JSR BDOS
	BCS WRFILX
WRFIL7	CLC
	LDA #16
	ADC BUFPNT
	STA BUFPNT
	LDA #0
	ADC BUFPNT+1
	STA BUFPNT+1
	INC D_CNT
	LDA D_CNT
	CMP S_CNT
	BCC WRFIL6
	CMP F_CNT
	BCC WRFILX
	LDA #READY
WRFILX	RTS

WRFIL1	CMP #NOCOPY
	BEQ WRFIL7
	JSR ERROR
	BCC WRFIL6
	RTS

WRFIL2	JSR ERROR
	BCC WRFIL3
	RTS


INIT	LDA #0			;INITIALIZE VARABLES
	LDY #$28
INIT11	STA 0,Y
	DEY
	BPL INIT11
	JSR RESP		;RESET BUFFER POINTER
	LDY #0			;SET SOURCE DRIVE
	LDA (FCB1),Y
	BNE INIT6
	LDX #GETDRV		;GET DEFAULT DRIVE
	JSR BDOS
	TAX
	INX
	TXA
INIT6	STA SDRIVE		;SAVE SOURCE DRIVE
	DEC SDRIVE
	ORA #$40
	STA SD_ASC
	LDY #0
	LDA (FCB2),Y		;CHECK FOR DESTINATION DRIVE
	BNE INIT7
	LDA SDRIVE
	TAX
	INX
	TXA
INIT7	STA DDRIVE		;SET DESTINATION DRIVE
	DEC DDRIVE
	ORA #$40
	STA DD_ASC
	CMP SD_ASC
	BNE INIT8
	LDA FLAG
	ORA #$80		;SET DISK CHANGE FLAG
	STA FLAG
INIT8	LDY #0
	LDA (DMA),Y		;CHECK FOR SWITCH
	STA PNT
INIT3	INY
	CPY PNT
	BCS INIT2
	LDA (DMA),Y
	CMP #'/			;SWITCH?
	BNE INIT3
	INY
	CPY PNT
	BCS INIT2
	LDA (DMA),Y		;GET SWITCH
	LDX #0
INIT4	CMP SWTAB,X
	BEQ INIT5
	INX
	INX
	CPX #SWTABX-SWTAB
	BCC INIT4
INIT2	LDA #0
	STA SWITCH
	CLC
	RTS

INIT1	LDA #NOFILENAME
	SEC
	RTS

INIT5	INX
	LDA SWTAB,X
	ORA SWITCH
	STA SWITCH
	CLC
	RTS


CH_SD	LDA SD_ASC
	STA DRIVE
	CMP #'E
	BCS CH_SD1
	BIT FLAG
	BPL CH_SD1
	LDA #SOURCE
	LDY #SOURCE/256
	JSR XCH_DSK		;CHANGE SOURCE DISK
CH_SD1	LDA SDRIVE
	LDX #SELDRV
	JSR BDOS
	BCC CH_SDX
	JSR ERROR
	BCC CH_SD1
CH_SDX	RTS


CH_DD	LDA DD_ASC		;CHANGE DESTINATION DISK
	STA DRIVE
	CMP #'E
	BCS CH_DD2
	BIT FLAG
	BPL CH_DD2
	LDA #DEST
	LDY #DEST/256
	JSR XCH_DSK
CH_DD2	LDA DDRIVE
	LDX #SELDRV
	JSR BDOS
	BCC CH_DDX
	JSR ERROR
	BCC CH_DD2
CH_DDX	RTS


RESP	LDA #BUFFER		;RESET BUFPNT and S_PNT
	STA BUFPNT
	STA S_PNT
	LDA #BUFFER/256
	STA BUFPNT+1
	STA S_PNT+1
	RTS


XCH_DSK	JSR PRTSTR
	LDA #XCHGM
	LDY #XCHGM/256		;INSERT DISK
	JSR PRTSTR
	LDY #_CHRIN		;WAIT FOR KEY
	JSR BIOS
	JSR CRLF
	CLC
	RTS


GETFIL	JSR FCB_CHECK		;GET FILE LIST TO COPY
	LDX #FIRST
	JSR BDOS		;CHECK FOR FILE
	BCC GETFIL1
	JSR ERROR
	BCC GETFIL
	RTS

GETFIL1	STY DIRPNT		;PRINT DIRECTORY ENTRY
	STY DIRPNT1
	TYA
	CLC
	ADC #DIR_EXT
	TAY
	LDA (DIRBUF),Y		;SKIP EXTENSIONS
	BNE GETFIL3
	BIT SWITCH		;CHECK Y/N SWITCH
	BPL GETFIL2
	LDA #' '		;print filename
	JSR PRTCHR
	LDA #8
	JSR PRT
	LDA #'.
	JSR PRTCHR
	LDA #3
	JSR PRT
	LDA #' '
	JSR PRTCHR
	LDA #MESSAGE
	LDY #MESSAGE/256
	JSR PRTSTR
	LDX #CONIN	;GET COMMAND KEY
	JSR BDOS
	AND #$5F	;LOWER --> UPPER CASE
	CMP #'E
	BEQ GETFIL9
	CMP #'Y
	BNE GETFIL5
GETFIL2	LDX #16
GETFIL7	LDY DIRPNT1
	LDA (DIRBUF),Y
	LDY #0
	STA (BUFPNT),Y
	INC DIRPNT1
	INC BUFPNT
	BNE GETFIL6
	INC BUFPNT+1
GETFIL6	DEX
	BNE GETFIL7
	INC F_CNT
	BIT SWITCH		;suppress CRLF
	BPL GETFIL3
GETFIL5	JSR CRLF
GETFIL3	LDX #NEXT
	JSR BDOS
	BCC GETFIL1
	CMP #NOTFND
	BEQ GETFIL9
	SEC
GETFILX	RTS

GETFIL9	LDA F_CNT
	BNE GETFIL8
	LDA #NO_FILE
	SEC
	RTS

GETFIL8	CLC
	RTS


PRT	STA CNT		;GET A CHR'S FROM DIRBUF
PRT1	INC DIRPNT	;AND PRINT THEM
	LDY DIRPNT
	LDA (DIRBUF),Y
	JSR PRTCHR	;PRINT CHR
	DEC CNT
	BNE PRT1
	RTS


FCB_CHECK

	LDY #1
	LDA (FCB1),Y	;CHECK FOR ARGUMENT
	CMP #SP
	BNE FCB_1
	LDA #'?		;SET FCB1 TO *.*
	LDY #11
FCB_2	STA (FCB1),Y
	DEY
	BNE FCB_2
FCB_1	RTS


SETSFCB	LDA #0			;SET SOURCE FCB
	TAY
	STA (FCB1),Y
SETSFC1	INY
	LDA (S_PNT),Y
	STA (FCB1),Y
	CPY #11
	BCC SETSFC1
	RTS


SETDFCB	LDA #0			;SET DESTINATION FCB1
	TAY
	STA (FCB1),Y
SETDFC1	INY
	LDA (BUFPNT),Y
	STA (FCB1),Y
	CPY #11
	BCC SETDFC1
	RTS


PRTFCB	JSR CRLF			;PRINT FILENAME IN FCB1
	LDY #1
PRTFCB1	LDA (FCB1),Y
	JSR PRTCHR
	INY
	CPY #9
	BCC PRTFCB1
	LDA #'.
	JSR PRTCHR
PRTFCB2	LDA (FCB1),Y
	JSR PRTCHR
	INY
	CPY #12
	BCC PRTFCB2
	RTS


MAKFIL	LDX #CREATE		;MAKE OR OPEN FILE
	JSR BDOS
	BCC MAKFILX
	CMP #DOUBLE
	BEQ MAKFIL1
	SEC
MAKFILX	RTS

MAKFIL1	LDA #ASKCOPY
	LDY #ASKCOPY/256
	JSR PRTSTR
	LDX #CONIN
	JSR BDOS
	CMP #'y
	BEQ MAKFIL2
	CMP #'Y
	BEQ MAKFIL2
	LDA #NOCOPY
	SEC
	RTS

MAKFIL2	LDX #OPEN
	JSR BDOS
	RTS


RESDMA	LDA #0			;RESET DMA
	STA DMA
	LDA 1PAGE
	STA DMA+1
	RTS


BIOS	LDX #BIOSC		;BIOS CALL
	JMP BDOS


ERROR	STA ERRNO	;ERROR ROUTINE
	JSR CRLF
	LDA #ERRTAB	;SET CCPV
	STA CCPV
	LDA #ERRTAB/256
	STA CCPV+1
	LDY #0  	;CHECK ERROR CODE
	LDA (CCPV),Y
ERROR1	BEQ ERROR4	;END OF ERROR ROUTINE
	CMP ERRNO
	BEQ ERROR3
ERROR2	JSR INCCPV	;SKIP ERROR MESSAGE
	BNE ERROR2
	JSR INCCPV
	JMP ERROR1

ERROR3	JSR INCCPV
	PHA
	JSR ERRTYP	;PRINT ERROR TYPE
	JSR INCCPV
	LDA CCPV	;PRINT ERROR MESSAGE
	LDY CCPV+1
	JSR PRTSTR
	PLA
	BPL ERROR5
	JSR ASKRTY	;ASK FOR RETRY
	LDA ERRNO
	RTS

ERROR4  JSR UETYP	;UNKNOWN ERROR
ERROR5  SEC
	RTS


ERRTYP  LDY #0  	;PRINT ERROR TYPE
ERRTY1  LSR A   	;SHIFT SOURCE BIT IN C
	BCS ERRTY2
	INY		;X= X+5
	INY
	INY
	INY
	INY
	BNE ERRTY1

ERRTY2  LDA ETYPTB,Y
	BEQ UETYP	;LAST CHR?
	JSR PRTCHR
	INY
	BNE ERRTY2

UETYP   LDA #ERRM1
	LDY #ERRM1/256
	JSR PRTSTR
	LDA ERRNO
	JSR PRTHEX

CRLF	LDA #CRLFM	;PRINTS A NEWLINE
	LDY #CRLFM/256

PRTSTR  LDX #STROUT
	JMP BDOS


PRT2SP  JSR PRTSP
PRTSP   LDA #SP
PRTCHR  LDX #CONOUT	;PRINTS A CHR
	JMP BDOS


INCCPV  INC CCPV	;INC CCPV
	BNE INCCP1
	INC CCPV+1
INCCP1  LDA (CCPV),Y
	RTS


ASKRTY  LDA #RTYMES	;RETRY?
	LDY #RTYMES/256
	JSR PRTSTR
	LDX #CONIN
	JSR BDOS
	CMP #'y
	BEQ ASKRT1
	CMP #'Y
	BEQ ASKRT1
	SEC
	RTS

ASKRT1  CLC
	RTS


PRTHEX  PHA		;PRINTS A HEX NUMBER
	LSR A
	LSR A
	LSR A
	LSR A
	JSR PRTNIB	;PRINT NIBBLE
	PLA
	AND #$0F

PRTNIB  CMP #$0A
	BCC PRTNI1
	ADC #6
PRTNI1  ADC #$30
	JSR PRTCHR
	RTS

;======   MESSAGES  ======

ERRM1   DB ' Error $',EOT
RTYMES  DB CR,LF,'Retry  (Y/N) ? ',EOT

ETYPTB  DB 'BIOS',$00,'BDOS',$00,'CCP',$00,$00
	DB 'RSX',$00,$00,'USER',$00

ERRTAB  DB $FD,$81,'Drive not ready',EOT
	DB $F9,$01,'Invalid Drive',EOT
	DB $DE,$02,'Directory full',EOT
	DB $DD,$82,'File not found',EOT
	DB $DC,$02,'File exists',EOT
	DB $D9,$02,'Invalid FCB',EOT
	DB $D8,$02,'Disk full',EOT
	DB $D6,$02,'File is R/O',EOT
	DB $81,$10,'*** COPY aborted ***',EOT
	DB $82,$10,'Switch missing',EOT
	DB $83,$10,'Argument missing',CR,LF
	DB 'Usage:   COPY d:filename.ext d: /switch',CR,LF
	DB 'Switch     Y   Y/N request for every filename',CR,LF,EOT
	DB $84,$10,'no file found',EOT
	DB $85,$10,'file too long to copy',EOT
	DB $00

;-----	TABLES  --------------------------------

SWTAB	DB 'Y',$80
SWTABX

;MESSAGES

MESSAGE
	DB '  ^C=abort, E=Exit, Y/N  ',EOT

STARTM	DB CR,LF,CR,LF,'COPY V1.4                '
	DB '  (c) D.Lausberg   1995',CR,LF,CR,LF,EOT
ENDM	DB CR,LF,CR,LF,'COPY finished',CR,LF,EOT
SOURCE	DB CR,LF,CR,LF,'Insert source',EOT
DEST	DB CR,LF,CR,LF,'Insert destination',EOT
XCHGM	DB ' disc to drive '
DRIVE	DB 'A: and press any key ',EOT
READING	DB CR,LF,'reading ...',EOT
WRITING	DB CR,LF,'writing ...',EOT
ASKCOPY	DB '  File exists, overwrite? (Y/N) ',EOT
CRLFM	DB CR,LF,EOT


BUFFER

	END
