;ERASE.COM   -  ERASE Utility
;by D. Lausberg		(c) 1991

;V1.0	14.01.91
;V1.1	 2.11.91	improved disc handling
;V1.2	25.03.95	long files error corrected
;V1.3	31.03.95	no disc change on drive C:
;V1.4	12.04.02	sector BIOS version
;V1.5	30.04.21	4 Floppies

;---- System Constants

DIRBUF	= $FC
FCB1	= $F6
BDOS	= $F0
CCPV	= $DE

;---- PAGE 00 CELLS

DIRPNT	= 0		;DIRECTORY POINTER
CNT	= 1		;COUNTER
F_CNT	= 2		;FILE COUNTER
BUFPNT	= 3		;FILE BUFFER POINTER
DIRPNT1	= 5		;RESERVE DIRPNT
ERRNO	= 6		;BUFFER FOR ERROR NUMBER

;---- BDOS COMMANDS

CONIN	= $01		;CONSOLE INPUT
CONOUT	= $02		;CONSOLE OUTPUT
STROUT	= $09		;PRINT STRING
FIRST	= $11		;GET 1. DIR ENTRY
NEXT	= $12		;GET NEXT DIR ENTRY
F_ERASE	= $13		;DELETE FILE
GETDRV	= $19		;GET DEFAULT DRIVE

;---- CONSTANTS

EOT	= $00
CR	= $0D
LF	= $0A
SP	= $20

DIR_EXT	= $0C

NOTFND	= $DD


;----------------------------------------------

ERASE	LDA #START_MESS	;GREETING MESSAGE
	LDY #START_MESS/256
	JSR PRTSTR
	LDA #0
	STA F_CNT	;RESET FILE COUNTER
	LDA #BUF
	STA BUFPNT	;SET BUFFER POINTER
	LDA #BUF/256
	STA BUFPNT+1
	CLC
	LDY #0
	LDA (FCB1),Y
	BNE ERASE9
	LDX #GETDRV
	JSR BDOS
	SEC
ERASE9	ADC #$40
	STA DRIVE
	JSR XCH_DSK
	LDY #1
	LDA (FCB1),Y	;CHECK FOR ARGUMENT
	CMP #SP
	BEQ ERASE4
	LDX #FIRST
	JSR BDOS	;CHECK FOR FILE
	BCC ERASE1
	JSR ERROR
	BCC ERASE
	RTS

ERASE4	LDA #HELP
	LDY #HELP/256
	JSR PRTSTR
	JMP ERASEX

ERASE1	STY DIRPNT
	TYA
	CLC
	ADC #DIR_EXT
	TAY
	LDA (DIRBUF),Y	;check for extensions
	BNE ERASE8
	LDY DIRPNT
	LDA (DIRBUF),Y
	INY
	STY DIRPNT1
	AND #$40	
	BEQ ERASE3	;CHECK FOR READ ONLY
	LDA #'*		;PRINT R/O-MARK
	JMP ERASE2

ERASE3	LDA #' '
ERASE2	JSR PRTCHR
	LDA #8
	JSR PRT
	LDA #'.
	JSR PRTCHR
	LDA #3
	JSR PRT
	LDA #' '
	JSR PRTCHR
	LDA #MESSAGE
	LDY #MESSAGE/256
	JSR PRTSTR
	LDX #CONIN	;GET COMMAND KEY
	JSR BDOS
	AND #$5F	;LOWER --> UPPER CASE
	CMP #'E
	BEQ ERA
	CMP #'Y
	BNE ERASE5
	LDX #11
ERASE7	LDY DIRPNT1
	LDA (DIRBUF),Y
	LDY #0
	STA (BUFPNT),Y
	INC DIRPNT1
	INC BUFPNT
	BNE ERASE6
	INC BUFPNT+1
ERASE6	DEX
	BNE ERASE7
	INC F_CNT
ERASE5	JSR CRLF
ERASE8	LDX #NEXT
	JSR BDOS
	BCC ERASE1
	CMP #NOTFND
	BEQ ERA
	JMP ERROR

ERA	DEC F_CNT
	BMI ERASEX
	LDA #11
	STA DIRPNT
ERA2	LDY #0
	LDA BUFPNT
	BNE ERA1
	DEC BUFPNT+1
ERA1	DEC BUFPNT
	LDA (BUFPNT),Y
	LDY DIRPNT
	STA (FCB1),Y
	DEC DIRPNT
	BNE ERA2
ERA3	LDX #F_ERASE
	JSR BDOS
	BCC ERA
	JSR ERROR
	BCC ERA3
	RTS

ERASEX	LDA #END_MESS
	LDY #END_MESS/256
	JSR PRTSTR
	RTS


PRT	STA CNT		;GET A CHR'S FROM DIRBUF
PRT1	INC DIRPNT	;AND PRINT THEM
	LDY DIRPNT
	LDA (DIRBUF),Y
	JSR PRTCHR	;PRINT CHR
	DEC CNT
	BNE PRT1
	RTS


XCH_DSK	LDA DRIVE
	CMP #'E
	BCS XCHDSKX
	LDA #XCHGM
	LDY #XCHGM/256		;INSERT DISK
	JSR PRTSTR
	LDX #CONIN		;WAIT FOR KEY
	JSR BDOS
	JSR CRLF
XCHDSKX	CLC
	RTS


ERROR	STA ERRNO	;ERROR ROUTINE
	JSR CRLF
	LDA #ERRTAB	;SET CCPV
	STA CCPV
	LDA #ERRTAB/256
	STA CCPV+1
	LDY #0  	;CHECK ERROR CODE
	LDA (CCPV),Y
ERROR1	BEQ ERROR4	;END OF ERROR ROUTINE
	CMP ERRNO
	BEQ ERROR3
ERROR2	JSR INCCPV	;SKIP ERROR MESSAGE
	BNE ERROR2
	JSR INCCPV
	JMP ERROR1

ERROR3	JSR INCCPV
	PHA
	JSR ERRTYP	;PRINT ERROR TYPE
	JSR INCCPV
	LDA CCPV	;PRINT ERROR MESSAGE
	LDY CCPV+1
	JSR PRTSTR
	PLA
	BPL ERROR5
	JSR ASKRTY	;ASK FOR RETRY
	LDA ERRNO
	RTS

ERROR4  JSR UETYP	;UNKNOWN ERROR
ERROR5  SEC
	RTS


ERRTYP  LDY #0  	;PRINT ERROR TYPE
ERRTY1  LSR A   	;SHIFT SOURCE BIT IN C
	BCS ERRTY2
	INY		;X= X+5
	INY
	INY
	INY
	INY
	BNE ERRTY1

ERRTY2  LDA ETYPTB,Y
	BEQ UETYP	;LAST CHR?
	JSR PRTCHR
	INY
	BNE ERRTY2

UETYP   LDA #ERRM1
	LDY #ERRM1/256
	JSR PRTSTR
	LDA ERRNO
	JSR PRTHEX

CRLF	LDA #CRLFM	;PRINTS A NEWLINE
	LDY #CRLFM/256

PRTSTR  LDX #STROUT
	JMP BDOS

PRT2SP  JSR PRTSP
PRTSP   LDA #SP
PRTCHR  LDX #CONOUT	;PRINTS A CHR
	JMP BDOS


INCCPV  INC CCPV	;INC CCPV
	BNE INCCP1
	INC CCPV+1
INCCP1  LDA (CCPV),Y
	RTS


ASKRTY  LDA #RTYMES	;RETRY?
	LDY #RTYMES/256
	JSR PRTSTR
	LDX #CONIN
	JSR BDOS
	CMP #'y
	BEQ ASKRT1
	CMP #'Y
	BEQ ASKRT1
	SEC
	RTS

ASKRT1  CLC
	RTS


PRTHEX  PHA		;PRINTS A HEX NUMBER
	LSR A
	LSR A
	LSR A
	LSR A
	JSR PRTNIB	;PRINT NIBBLE
	PLA
	AND #$0F

PRTNIB  CMP #$0A
	BCC PRTNI1
	ADC #6
PRTNI1  ADC #$30
	JSR PRTCHR
	RTS

;======   MESSAGES  ======

ERRM1   DB ' Error $',EOT
RTYMES  DB CR,LF,'Retry  (Y/N) ? ',EOT

ETYPTB  DB 'BIOS',$00,'BDOS',$00,'CCP',$00,$00
	DB 'RSX',$00,$00,'USER',$00

ERRTAB  DB $FD,$81,'Drive not ready',EOT
	DB $F9,$01,'Invalid Drive',EOT
	DB $DE,$02,'Directory full',EOT
	DB $DD,$82,'File not found',EOT
	DB $DC,$02,'File exists',EOT
	DB $D9,$02,'Invalid FCB',EOT
	DB $D8,$02,'Disk full',EOT
	DB $D6,$02,'File is R/O',EOT

	DB $00

CRLFM	DB CR,LF,EOT

MESSAGE
	DB '  ^C=abort (nothing deleted), E=Exit, Y/N  ',EOT

START_MESS
	DB CR,LF,'ERASE V1.5      (c) D.Lausberg',CR,LF,EOT
END_MESS
	DB CR,LF,'ERASE finished',CR,LF,EOT
HELP	DB CR,LF,' usage d:ERASE d:filename.ext',CR,LF,EOT
XCHGM	DB CR,LF,'Insert disc to drive '
DRIVE	DB 'B:',CR,LF,'and press any key ',CR,LF,EOT


BUF

	END
